plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'org.jetbrains.intellij' version '0.4.9'
}

repositories {
    maven { url 'https://cache-redirector.jetbrains.com/intellij-repository/snapshots' }
    maven { url 'https://cache-redirector.jetbrains.com/maven-central' }
}

version = version != 'unspecified' ? version : '0.0.0.1'

if (!project.hasProperty('configuration')) ext.configuration = 'Debug'

intellij {
    type = 'RD'
    // https://www.jetbrains.com/intellij-repository/releases
    version = '2020.1.0'
    downloadSources = false
}

compileKotlin {
    kotlinOptions { jvmTarget = "1.8" }
}

prepareSandbox {
    from("$projectDir/../$gradle.resharperPluginProjectName/bin/RD20201/$configuration", {
        into "$intellij.pluginName/dotnet"
        include "$gradle.resharperPluginProjectName*"
    })
}

patchPluginXml {
    def historyText = file("$projectDir/../../History.md").text
    def historyMatches = historyText =~ /(?s)###(.+?)###(.+?)(?=###|$)/

    changeNotes = historyMatches.collect {
        def versionTitle = it[1]
        def versionText = it[2].replaceAll(/(?s)\r?\n/, "<br />\n")
        "<b>$versionTitle</b>$versionText"
    }.take(10).join('')

    //sinceBuild = "1xx.*"
}

runIde {
    configDirectory = System.properties['user.home'] + '/.RiderPluginDev/config'
    systemDirectory = System.properties['user.home'] + '/.RiderPluginDev/system'
}
